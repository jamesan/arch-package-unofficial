# Maintainer: James An <james@jamesan.ca>
# Contributor: Maxime Poulin <maxpoulin64@gmail.com>
# Contributor: Arnaud Taffanel <dev@taffanel.org>
# Contributor: Victor HÃ¤ggqvist <victor@snilius.com>

_pkgname=Solaar
pkgname=${_pkgname,}-git
pkgver=0.9.2.r84.g1bfced5
pkgrel=1
pkgdesc="Linux devices manager for the Logitech Unifying Receiver."
arch=('any')
url="http://pwr.github.io/$_pkgname"
license=('GPL2')
depends=('python-pyudev' 'python-gobject')
makedepends=('git')
provides=("${_pkgname,}=$pkgver")
conflicts=("${_pkgname,}")
install="${_pkgname,}.install"
source=("$_pkgname"::"git+https://github.com/decibyte/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  (
    set -o pipefail
    git describe --long --tag | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

package() {
  cd $_pkgname
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 rules.d/42-logitech-unify-permissions.rules \
    "$pkgdir/etc/udev/rules.d/42-logitech-unify-permissions.rules"
}
